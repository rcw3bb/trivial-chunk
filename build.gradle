plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.javamodularity.moduleplugin' version '1.8.10'
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
    id 'signing'
    id 'jacoco'
    id 'pmd'
}

group 'xyz.ronella.casual'
version '2.10.0'

sourceCompatibility = 1.11

pmd {
    consoleOutput = true
    toolVersion = "6.45.0"
    rulesMinimumPriority = 5
    ruleSetFiles = files('config/pmd/java/custom.xml')
    ruleSets = []
}

repositories {
    if (Boolean.valueOf(USE_PRIVATE_REPO)) {
        println "Using Private Repository"

        maven {
            url 'https://repo.ronella.xyz/artifactory/java-central'
            credentials {
                username "${artifactoryUsername}"
                password "${artifactoryPassword}"
            }
            mavenContent {
                releasesOnly()
            }
        }
        maven {
            url 'https://repo.ronella.xyz/archiva/repository/snapshots/'
            credentials {
                username "${archivaUsername}"
                password "${archivaPassword}"
            }
            mavenContent {
                snapshotsOnly()
            }
        }
    }
    else {
        println "Using mavenCentral Repository"
        mavenCentral()
    }
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task testsJar(type: Jar, dependsOn: [testClasses]) {
    archiveClassifier = 'tests'
    from sourceSets.test.output
}

jar.finalizedBy(javadocJar, sourcesJar, testsJar)

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        repositories.maven {
            def publishingPath = version.toString().contains("SNAPSHOT") ? 'snapshots' : 'internal'
            url "https://repo.ronella.xyz/archiva/repository/${publishingPath}/"
            credentials {
                username "${archivaUsername}"
                password "${archivaPassword}"
            }
        }
        mavenJava(MavenPublication) {
            from components.java
            artifact testsJar
            pom {
                name = 'Trivial Chunk'
                description = 'A library of trivial codes.'
                url = 'https://github.com/rcw3bb/trivial-chunk'
                properties = [
                        "inceptionYear": '2019'
                ]
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/rcw3bb/trivial-chunk/blob/master/LICENSE.md'
                    }
                }
                developers {
                    developer {
                        id = 'rcw3bb'
                        name = 'Ronaldo Webb'
                        email = 'ronaldo.c.webb@gmail.com'
                    }
                }
                scm {
                    connection = 'https://github.com/rcw3bb/trivial-chunk.git'
                    developerConnection = 'https://github.com/rcw3bb/trivial-chunk.git'
                    url = 'https://github.com/rcw3bb/trivial-chunk'
                }
            }
        }
    }
}

test {
    useJUnitPlatform()
    dependsOn pmdMain
    finalizedBy jacocoTestReport
}

pmdTest.enabled = false

jacocoTestReport {
    dependsOn test
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl = uri('https://oss.sonatype.org/service/local/')
            snapshotRepositoryUrl = uri('https://oss.sonatype.org/content/repositories/snapshots/')
            stagingProfileId = "142f3cca5418da"
        }
    }
}

dependencies {
    testImplementation 'org.mockito:mockito-inline:4.5.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.8.2'
}

closeAndReleaseSonatypeStagingRepository.dependsOn('publishToSonatype')